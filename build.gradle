// https://docs.gradle.org/current/userguide/tutorial_java_projects.html

buildscript {
    /**
     * Necessary because the applied plugins need their
     * dependencies resolved before anything else.
     **/
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://repo.spring.io/milestone' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.0.RC1")
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.11'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

checkstyle {
    toolVersion "7.2"
    // output errors but continue build
    ignoreFailures true
}

sourceCompatibility = 1.8
version = '0.1.0'

/**
 * Probably just want to run the jar directly
 * via java -jar build/libs/java-cli-0.1.0.jar
 * because while this works, the input gets
 * garbled because gradle holds onto output.
 **/
bootRun {
    standardInput = System.in
}

jar {
    baseName = 'java-cli'
    manifest {
        attributes 'Implementation-Title': 'java-cli',
        'Implementation-Version': version
    }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.shell:spring-shell-starter:2.0.1.BUILD-SNAPSHOT'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/libs-snapshot' }
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
}
